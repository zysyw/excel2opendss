Clear
! define circuit
new circuit.CalcLineLoss basekv=115 1.00 0.0 50.0 3 20000 21000 4.0 3.0
set basefrequency=50

! 10kV配网线路OpenDSS计算开发版
  ! 参数库
new linecode.LGJ-16 R1=2.04 X1=0.4
new linecode.LGJ-25 R1=1.38 X1=0.38
new linecode.LGJ-35 R1=0.85 X1=0.37
new linecode.LGJ-50 R1=0.65 X1=0.36
new linecode.LGJ-70 R1=0.45 X1=0.35
new linecode.LGJ-95 R1=0.33 X1=0.34
new linecode.LGJ-120 R1=0.27 X1=0.33
new linecode.LGJ-150 R1=0.21 X1=0.32
new linecode.LGJ-185 R1=0.17 X1=0.31
new linecode.LGJ-240 R1=0.132 X1=0.31
new linecode.LGJ-300 R1=0.107 X1=0.3
new linecode.LGJ-400 R1=0.08 X1=0.28
new XfmrCode.S9-50 phases=3 windings=2 conns=[delta, wye] kVs=[10.5, 0.4] kVAs=[50, 50] XHL=4 %imag=2 %noloadloss=0.34 %loadloss=1.74
new XfmrCode.S9-100 phases=3 windings=2 conns=[delta, wye] kVs=[10.5, 0.4] kVAs=[100, 100] XHL=4 %imag=1.6 %noloadloss=0.29 %loadloss=1.5
new XfmrCode.S9-160 phases=3 windings=2 conns=[delta, wye] kVs=[10.5, 0.4] kVAs=[160, 160] XHL=4 %imag=1.4 %noloadloss=0.25 %loadloss=1.375
new XfmrCode.S9-200 phases=3 windings=2 conns=[delta, wye] kVs=[10.5, 0.4] kVAs=[200, 200] XHL=4 %imag=1.3 %noloadloss=0.24 %loadloss=1.3
  ! 配电线路
! substation transformer
new transformer.sub phases=3 windings=2 buses=[SourceBus,  nc10kVbus] kvs=[115, 10.5] kvas=[20000, 20000] XHL=7
new line.L1 bus1=nc10kVbus bus2=bus1 Linecode=LGJ-70 Length=0.8
new line.L2 bus1=bus1 bus2=bus2 Linecode=LGJ-70 Length=0.7
new line.L3 bus1=bus2 bus2=bus3 Linecode=LGJ-70 Length=1
new line.L4 bus1=bus2 bus2=bus21 Linecode=LGJ-70 Length=0.3
new line.L5 bus1=bus21 bus2=bus22 Linecode=LGJ-70 Length=0.5
new transformer.T1 Buses=[bus1, T1] XfmrCode=S9-50
new transformer.T21 Buses=[bus21, T21] XfmrCode=S9-160
new transformer.T22 Buses=[bus22, T22] XfmrCode=S9-200
new transformer.T3 Buses=[bus3, T3] XfmrCode=S9-100
  ! 运行数据
new load.T1 bus1=T1 kv=0.4 kw=25 kvar=12.5
new load.T21 bus1=T21 kv=0.4 kw=80 kvar=40
new load.T22 bus1=T22 kv=0.4 kw=100 kvar=50
new load.T3 bus1=T3 kv=0.4 kw=50 kvar=25
